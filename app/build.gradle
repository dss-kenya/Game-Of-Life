apply plugin: 'com.android.application'
apply from: '../jacoco.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "com.dhara.gameoflife"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }
    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod {

        }
    }
    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }
    /*compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }*/
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'io.reactivex:rxandroid:1.2.0'

    compile "com.android.support.test.espresso:espresso-idling-resource:2.2.1"

    // Dependencies for local unit tests
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-all:1.10.19"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.powermock:powermock-module-junit4:1.6.2"
    testCompile "org.powermock:powermock-api-mockito:1.6.2"

    // Android Testing Support Library's runner and rules
    androidTestCompile "com.android.support.test:runner:0.4.1"
    androidTestCompile "com.android.support.test:rules:0.4.1"

    // Espresso UI Testing dependencies.
    androidTestCompile "com.android.support.test.espresso:espresso-core:2.2.1"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:2.2.1"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:2.2.1"

    androidTestCompile 'junit:junit:4.12'
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-v4:23.4.0"
    resolutionStrategy.force "com.android.support:recyclerview-v7:23.4.0"
    resolutionStrategy.force "com.android.support:support-annotations:23.4.0"
}
